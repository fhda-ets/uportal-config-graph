plugins {
    id "java"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation "info.picocli:picocli:3.9.1"
    implementation "org.fusesource.jansi:jansi:1.17.1"
    implementation "com.squareup.okhttp3:okhttp:3.12.1"
    implementation "io.jsonwebtoken:jjwt-api:0.10.5"
    implementation "io.jsonwebtoken:jjwt-impl:0.10.5"
    implementation "io.jsonwebtoken:jjwt-jackson:0.10.5"
}

// Disable uPortal-start tasks for root project
if(project.tasks.findByName("tomcatClean")) {
    tomcatClean.enabled = false
}

if(project.tasks.findByName("tomcatDeploy")) {
    tomcatDeploy.enabled = false
}

class ImportTask extends DefaultTask {

    ImportTask() {
        this.group = "Config Graph (Foothill-De Anza)"
        this.dependsOn("classes")
    }

    String action = "import-dir"
    File input = project.rootProject.file("./config-graph")
    File portalHome = project.rootProject.file(".gradle/tomcat/portal")

    @Option(option = "input", description = "Set the graph file or directory of files to import")
    void setInput(String path) {
        this.input = project.file(path)
        this.action = (this.input.file) ? "import-file" : "import-dir"
    }

    @Option(option = "portal-home", description = "Set the portal home directory")
    void setPortalHome(String path) {
        this.portalHome = project.file(path)
    }

    @TaskAction
    def perform() {
        project.javaexec {
            classpath = project.convention.plugins.java.sourceSets.main.runtimeClasspath
            main = "edu.fhda.uportal.confgraph.cli.CliRunner"
            workingDir = project.rootProject.rootDir
            args "--portal-home", portalHome.absolutePath
            args action
            args input.absolutePath
        }
    }

}

task configGraphImport(type: ImportTask)